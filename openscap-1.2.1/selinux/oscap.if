
## <summary>policy for oscap</summary>

########################################
## <summary>
##	Transition to oscap.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`oscap_probe_template',`
	gen_require(`
		type oscap_t, oscap_exec_t;
	')

	type $1_oscap_probe_t;
	type $1_oscap_probe_exec_t;
	application_domain($1_oscap_probe_t, $1_oscap_probe_exec_t)
	role system_r types $1_oscap_probe_t;

	corecmd_search_bin($1_oscap_probe_t)
	domtrans_pattern(oscap_t, $1_oscap_probe_exec_t, $1_oscap_probe_t)
')

########################################
## <summary>
##	Transition to oscap.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`oscap_domtrans',`
	gen_require(`
		type oscap_t, oscap_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, oscap_exec_t, oscap_t)
')

########################################
## <summary>
##	Execute oscap in the oscap domain, and
##	allow the specified role the oscap domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the oscap domain.
##	</summary>
## </param>
#
interface(`oscap_run',`
	gen_require(`
		type oscap_t, oscap_tmp_t;
	')

	oscap_domtrans($1)
	role $2 types oscap_t;

	manage_dirs_pattern($1, oscap_tmp_t, oscap_tmp_t)
	manage_files_pattern($1, oscap_tmp_t, oscap_tmp_t)
')

########################################
## <summary>
##	Role access for oscap
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`oscap_role',`
	gen_require(`
		type oscap_t;
	')

	role $1 types oscap_t;

	oscap_domtrans($2)

	ps_process_pattern($2, oscap_t)
	allow $2 oscap_t:process signal;
')
